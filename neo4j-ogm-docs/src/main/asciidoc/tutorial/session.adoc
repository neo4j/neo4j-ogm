[[tutorial-session]]
= Interacting with the model

So our domain entities are annotated, now we're ready persist them to the graph!

== Sessions

The smart object mapping capability is provided by the `Session` object.
A `Session` is obtained from a `SessionFactory`.

We're going to set up the `SessionFactory` just once and have it produce as many sessions as required.

[source, groovy]
----
public class Neo4jSessionFactory {

    private final static Configuration = ... // provide configuration as seen before
    private final static SessionFactory sessionFactory = new SessionFactory(configuration, "school.domain");
    private static Neo4jSessionFactory factory = new Neo4jSessionFactory();

    public static Neo4jSessionFactory getInstance() {
        return factory;
    }

    // prevent external instantiation
    private Neo4jSessionFactory() {
    }

    public Session getNeo4jSession() {
        return sessionFactory.openSession();
    }
}
----


The `SessionFactory` constructor accepts packages that are to be scanned for annotated domain entities.

The domain objects in our university application are grouped under `school.domain`.
When the `SessionFactory` is created, it will scan `school.domain` for potential domain classes and construct the object mapping metadata to be used by all sessions created thereafter.

NOTE: We use here the `SessionFactory` with the package of domain classes as a parameter.
This sets up an in-memory embedded database.
In your application, you would also pass the configuration to connect to your actual database.

The `Session` keeps track of changes made to entities and relationships and persists ones that have been modified on save.
Once an entity is tracked by the session, reloading this entity within the scope of the same session will result in the session cache returning the previously loaded entity.
However, the subgraph in the session will expand if the entity or its related entities retrieve additional relationships from the graph.

For the purpose of this demo application, we'll use short living sessions - a new session per web request - to avoid stale data issues.

Our university application will use the following operations:

[source, groovy]
----
interface Service<T> {

    Iterable<T> findAll()

    T find(Long id)

    void delete(Long id)

    T createOrUpdate(T object)

}
----

These CRUD interactions with the graph are all handled by the `Session`.
Let's write a `GenericService` to deal with common `Session` operations.

[source, groovy]
----
abstract class GenericService<T> implements Service<T> {

    private static final int DEPTH_LIST = 0
    private static final int DEPTH_ENTITY = 1
    protected Session session = Neo4jSessionFactory.getInstance().getNeo4jSession()

    @Override
    Iterable<T> findAll() {
        return session.loadAll(getEntityType(), DEPTH_LIST)
    }

    @Override
    T find(Long id) {
        return session.load(getEntityType(), id, DEPTH_ENTITY)
    }

    @Override
    void delete(Long id) {
        session.delete(session.load(getEntityType(), id))
    }

    @Override
    T createOrUpdate(T entity) {
        session.save(entity, DEPTH_ENTITY)
        return find(entity.id)
    }

    abstract Class<T> getEntityType()
}
----

One of the features of Neo4j-OGM is variable depth persistence.
This means you can vary the depth of fetches depending on the shape of your data and application.
The default depth is 1, which loads simple properties of the entity and its immediate relations.
This is sufficient for the `find` method, which is used in the application to present a create or edit form for an entity.

image::classDetail.png[Class detail, 896, 457]

Loading relationships is not required when listing all entities of a type.
We merely require the id and name of the entity, and so a depth of 0 is used by `findAll` to only load simple properties of the entity but skip its relationships.

image::departmentListing.png[Department listing, 896, 457]

The default save depth is -1, or everything that has been modified and can be reached from the entity up to an infinite depth.
This means we can persist all our changes in one go.

This `GenericService` takes care of CRUD operations for all our entities!
All we did was delegate to the `Session`; no need to write persistence logic for every entity.


== Queries

Popular Study Buddies is a report that lists the most popular peer study groups.
This requires a custom Cypher query.
It is easy to supply a Cypher query to the `query` method available on the `Session`.

[source, groovy]
----
class StudyBuddyServiceImpl extends GenericService<StudyBuddy> implements StudyBuddyService {

    @Override
    Iterable<StudyBuddy> findAll() {
        return session.loadAll(StudyBuddy, 1)
    }

    @Override
    Iterable<Map<String, Object>> getStudyBuddiesByPopularity() {
        String query = "MATCH (s:StudyBuddy)<-[:BUDDY]-(p:Student) return p, count(s) as buddies ORDER BY buddies DESC"
        return Neo4jSessionFactory.getInstance().getNeo4jSession().query(query, Collections.EMPTY_MAP)
    }

    @Override
    Class<StudyBuddy> getEntityType() {
        return StudyBuddy.class
    }
}
----

The `query` provided by the `Session` can return a domain object, a collection of them, or a special wrapped object called a `Result`.




